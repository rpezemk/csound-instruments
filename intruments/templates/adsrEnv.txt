;############### ENVELOPE #################
instr 21 
    iAtt init  p4 ;A
    iDec init  p5 ;D
    iSus init  p6 ;S
    iRel init  p7 ;R
    iChan init p8 ;Ch

    kTime times
    kSavedEnv init 0
    kEnv init 0
    kAttTimer init 0
    kDecTimer init 0
    kRelTimer init 0

    kAnyPressed chnget "KEY_PRESSED"

    SRetriggerName sprintf "%s%d", "ENV_RETRIGGER_", iChan
    kRetrigg chnget SRetriggerName

    kPrevPressed init 0

    kAttSnap init 0
    kDecSnap init 0
    kRelSnap init 0

    kStateTrigger = 0 ; 1 -> A, 4 -> R /// ADSR

    kState init 0
    kPrevState init 0
    kDecTimeSaved init 0

    kNoteOnTrigger = max(kAnyPressed - kPrevPressed, 0)

    if kAnyPressed > kPrevPressed then
        kStateTrigger = 1
    endif

    if kPrevPressed > kAnyPressed then
        kStateTrigger = 4
    endif


    if kRetrigg == 1 && kAnyPressed == 1 then
        kStateTrigger = 1
    endif

    if kState == 1 || kState == 2 then
        kAttTimer = kTime - kAttSnap
    else
        kAttTimer = 0
    endif

    if kState == 2 then 
        kDecTimer = kTime - kDecSnap
    else 
        kDecTimer = 0
    endif

    if kState == 4  then
        kRelTimer = kTime - kRelSnap
    else
        kRelTimer = 0
    endif

    if kStateTrigger == 1  then
        kAttSnap = kTime
        kAttTimer = 0
        kState = 1
    endif

    if kState == 1 && kAttTimer >= iAtt then
        kDecSnap = kTime
        kState = 2
    endif

    if kState == 2 && kAttTimer > iAtt + iDec then
        kState = 3
    endif

    if kStateTrigger == 4 then
        kRelSnap = kTime
        kState = 4
    endif

    if kRelTimer > iRel then
        kState = 0
    endif

    if kState == 4 then
        if kPrevState == 1 || kPrevState == 2 || kPrevState == 3 then
            kSavedEnv = kEnv
        endif
    endif

    if kState == 1 then
        kEnv = kAttTimer / iAtt
    elseif kState == 2 then
        kEnv = iSus +  (1 - iSus) * (iDec - kDecTimer)/iDec 
    elseif kState == 3 then
        kEnv = iSus
    elseif kState == 4 then
        kRelPhase = (iRel - kRelTimer)/iRel  + (kSavedEnv - 1)
        kEnv = max(kRelPhase, 0)
    endif

    SResult sprintf "%s%d", "ENV_", iChan
    chnset kEnv, SResult

    kPrevState = kState
    kPrevPressed = kAnyPressed
    kAnyPressed = 0
    kNoteOnTrigger = 0

endin
